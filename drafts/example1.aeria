mixin './relativePath.aeria'        -- mixins can be included from the filesystem

mixin 'github:organization/mixin'   -- they can also be fetched remotely
mixin 'npm:@organization/mixin'     -- the content of the mixin will simply be concatenated

collection Person {                 -- collection names must be capitalized, they'll be later converted to camelCase
  required {
    first_name
    last_name
    responsible @cond(age < 18)
  }

  properties {
    first_name    str
    last_name     str
    age           int
    blocked       bool
    responsible   Person    @indexes([first_name, last_name])
    friends       Person[]  @indexes([first_name, last_name]) @constraints(blocked == false)
  }

  getters {
    full_name @macro:js (value) => {        -- macros can be used to output text as-it-is
      return `${first_name} ${last_name}`   -- the :js suffix is just a hint to the IDE to highlight the block as javascript
    }
    @end
  }

  table [         -- array are started and ended with []
    first_name,
    last_name,
    age
  ]
}

